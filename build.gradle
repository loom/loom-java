plugins {
    id 'java'
    id 'org.ec4j.editorconfig' version '0.0.3'
    id 'checkstyle'
}

allprojects {
    repositories {
        mavenCentral()
    }

    group = 'io.github.loom'
    version = artifactVersion
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.ec4j.editorconfig'
    apply plugin: 'checkstyle'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    java {
        withJavadocJar()
        withSourcesJar()
    }

    editorconfig {
        excludes = ["build"]
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.24'
        annotationProcessor 'org.projectlombok:lombok:1.18.24'
    }

    check.dependsOn editorconfigCheck

    checkstyle {
        configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
        configProperties = ["suppressionFile": "${project.rootDir}/config/checkstyle/checkstyle-suppressions.xml"]
        toolVersion = "8.40"
        ignoreFailures = false
        maxErrors = 0
        maxWarnings = 0
    }

    jar {
        manifest {
            attributes(
                'Specification-Title': artifactName,
                'Specification-Version': artifactVersion,
                'Specification-Vendor': 'io.github.loom',
                'Implementation-Title': artifactName,
                'Implementation-Version': artifactVersion,
                'Implementation-Vendor': 'io.github.loom'
            )
        }
    }

    publishing {
        repositories {
            maven {
                name "OSSRH"
                url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username System.getenv("MAVEN_USERNAME")
                    password System.getenv("MAVEN_PASSWORD")
                }
            }
        }

        publications {
            maven(MavenPublication) {
                artifactId artifactId
                from components.java
                pom {
                    name = artifactName
                    description = artifactDescription
                    url = "https://github.com/loom/loom-java"
                    licenses {
                        license {
                            name = "MIT License"
                            url = "https://github.com/loom/loom-java/blob/main/LICENSE"
                        }
                    }
                    developers {
                        developer {
                            id = "gyuwon"
                            name = "Gyuwon Yi"
                            email = "gyuwon@live.com"
                        }
                    }
                    scm {
                        connection = "https://github.com/loom/loom-java.git"
                        developerConnection = "https://github.com/loom/loom-java.git"
                        url = "https://github.com/loom/loom-java"
                    }
                }
            }
        }
    }

    signing {
        def signingKey = System.getenv("SIGNING_KEY")
        def signingPassword = System.getenv("SIGNING_PASSWORD")
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.maven
    }
}
